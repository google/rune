//  Copyright 2024 Google LLC.
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      https://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

/*
 * Type builder - convenience functions to create Type objects
 */

use sym
use typeclasses

func CHAR() -> Type {
  return Type(TypeName(Sym.new("char")))
}

func STRING() -> Type {
  return Type(TypeName(Sym.new("string")))
}

func BOOLEAN() -> Type {
  return Type(TypeName(Sym.new("bool")))
}

func NONETY() -> Type {
  return Type(TypeName(Sym.new("none")))
}

func INTEGER() -> Type {
  return Type(AnyIntType(true))
}

func UINTEGER() -> Type {
  return Type(AnyIntType(false))
}

func ANYINTEGER() -> Type {
  return Type(ChoiceType([INTEGER(), UINTEGER()]))
}

func NUMBER() -> Type {
  return Type(ChoiceType([INTEGER(), UINTEGER(), F(32), F(64)]))
}

func U(width: u64) -> Type {
  return Type(IntType(false, <u32>width))
}

func I(width: u64) -> Type {
  return Type(IntType(true, <u32>width))
}

func F(width: u64) -> Type {
  return Type(FloatType(<u32>width))
}

func V(id: u64) -> Type {
  return Type(TypeVar(<i32>id, null(Type)))
}

func BV(id: u64, constraint: Type) -> Type {
  return Type(TypeVar(<i32>id, constraint))
}

func TUPLE(types: [Type]) -> Type {
  return Type(TupleType(types))
}

func STRUCT(fields: [(string, Type)]) -> Type {
  return Type(StructType(fields))
}

func ARRAY(t: Type) -> Type {
  return Type(ArrayType(t))
}

func OR(types: [Type]) -> Type {
  return Type(ChoiceType(types))
}

func FN(arg: Type, res:Type) -> Type {
  return Type(FunctionType(arg, res))
}

func POLY(vars: u64 | [u64], scope: Type) -> Type {
  typeswitch vars {
    u64 => {
      return Type(PolymorphicType(Type(TypeVar(<i32>vars, null(Type))), scope))
    }
    [u64] => {
      boundvars = arrayof(Type)
      for v in vars {
        boundvars.append(Type(TypeVar(<i32>v, null(Type))))
      }
      return Type(PolymorphicType(boundvars, scope))
    }
  }
}

/*
 * Construct a bound polymorphic type -- where the typevariables are constrained
 * to be one of a list of types.
 *
 * Note that in the case of mulitple type variables, only the first is bound,
 * the remaining type variables are assumed to belong to one of the constraints.
 */
func BPOLY(vars: u64 | [u64], bounds: [Type], scope: Type) -> Type {
  if bounds.length() == 1 {
    constraint = bounds[0]
  } else {
    constraint = Type(ChoiceType(bounds))
  }
  typeswitch vars {
    u64 => {
      return Type(PolymorphicType(Type(TypeVar(<i32>vars, constraint)), scope))
    }
    [u64] => {
      boundvars = arrayof(Type)
      // The constraint only applies to the first typevariable
      for v in vars {
        boundvars.append(Type(TypeVar(<i32>v, constraint)))
        constraint = null(Type)
      }
      return Type(PolymorphicType(boundvars, scope))
    }
  }

}

